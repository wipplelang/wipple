[[no-std]]

use "sequence"

[help "A continuous range between two numbers."]
[help-group "Math"]
Range : N => type {
  min :: N
  max :: N
}

[help "A range between two numbers that counts up by a certain number."]
[help-group "Math"]
Stride : N => type {
  min :: N
  max :: N
  step :: N
}

N where (Default N) (Order N) (Add N N N) => instance (As-Sequence (Stride N) N) : range -> {
  zero : Default
  { min max step } : range

  (max >= min) . expect "`max` must be greater than `min`"
  (step >= zero) . expect "`step` must be positive"

  current : mutable min
  sequence {
    if (get current >= max) { None } {
      n : get current
      current . add! step
      Some n
    }
  }
}

[help "Form a range or stride between two numbers."]
[help-group "Math"]
to : syntax {
  'min to 'max by 'step -> Stride {
    min : 'min
    max : 'max
    step : 'step
  }
  'min to 'max -> Range {
    min : 'min
    max : 'max
  }
}
