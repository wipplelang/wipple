[[no-std]]

use "fp"
use "list"
use "logic"
use "util"
use "iterate"

[language "show"]
[help "Implement this trait to enable `show`ing values of your type."]
[help-group "Input and Output"]
Show : A => trait (A -> Text)

[help "Display a value on the screen."]
[help-group "Input and Output"]
[help-template "show (*text*)"]
[help-playground "https://wipple.dev/std/examples/show.txt"]
[diagnostic "accepts-text"]
[diagnostic-alias "print"]
[diagnostic-alias "println"]
[diagnostic-alias "display"]
show :: A where (Show A) => A -> ()
show : x -> intrinsic "display" (Show x)

instance (Show Text) : it
instance (Show Number) : n -> intrinsic "number-to-text" n
instance (Show Integer) : n -> intrinsic "integer-to-text" n
instance (Show Natural) : n -> intrinsic "natural-to-text" n
instance (Show Byte) : n -> intrinsic "byte-to-text" n
instance (Show Signed) : n -> intrinsic "signed-to-text" n
instance (Show Unsigned) : n -> intrinsic "unsigned-to-text" n
instance (Show Float) : n -> intrinsic "float-to-text" n
instance (Show Double) : n -> intrinsic "double-to-text" n
instance (Show Boolean) : x? -> if x? "True" "False"
instance (Show None-Error) : just "found `None`"

Value where (Show Value) => instance (Show (Maybe Value)) : x? -> when x? {
  Some x -> format "Some _" (Show x)
  None -> "None"
}

Success Failure where (Show Success) (Show Failure) => instance (Show (Result Success Failure)) : x? -> when x? {
  OK x -> format "OK _" x
  Error e -> format "Error _" e
}

Success Failure where (Show Failure) => instance (Expect (Result Success Failure) Success) : message -> x? -> when x? {
  OK x -> x
  Error e -> crash (format "_: _" message e)
}

[help "Implement the `read` function."]
[help-group "Input and Output"]
Read : A => trait (Text -> Maybe A)

[help "Parse `Text` into a value."]
[help-group "Input and Output"]
read :: A where (Read A) => Text -> Maybe A
read : Read

instance (Read Text) : Some
instance (Read Number) : text -> intrinsic "text-to-number" text
instance (Read Integer) : text -> intrinsic "text-to-integer" text
instance (Read Natural) : text -> intrinsic "text-to-natural" text
instance (Read Byte) : text -> intrinsic "text-to-byte" text
instance (Read Signed) : text -> intrinsic "text-to-signed" text
instance (Read Unsigned) : text -> intrinsic "text-to-unsigned" text
instance (Read Float) : text -> intrinsic "text-to-float" text
instance (Read Double) : text -> intrinsic "text-to-double" text

[help-group "Input and Output"]
[help "Read a value from the user with the provided prompt."]
prompt :: (A : Text) where (Read A) => Text -> A
prompt : prompt -> intrinsic "prompt" prompt (Read :: (Text -> Maybe A))

[help "Ask the user to pick from a set of options."]
[help-group "Input and Output"]
choice :: Element where (Show Element) => Text -> List Element -> Element
choice : prompt items -> {
  descriptions : items . transform Show . collect :: List Text
  index : intrinsic "choice" prompt descriptions
  items . nth index . unwrap
}
