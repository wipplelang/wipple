[[no-implicit-use]]

use "fp"
use "list"
use "logic"
use "util"
use "sequence"

[language "show"]
[help "Implement this trait to enable `show`ing values of your type."]
[help-group "Input and Output"]
[help-show-code]
[on-unimplemented ("cannot display _ on the screen" A)]
Show : (A : Text) => trait (A -> Text)

[help "Display a value on the screen."]
[help-group "Input and Output"]
[help-playground "https://wipple.dev/std/examples/show.txt"]
[diagnostic "accepts-text"]
[help-alias "print"]
[help-alias "println"]
[help-alias "display"]
[on-unresolved "`show` expects a value to be displayed"]
[resolve "insert a value to display" (show {%value%})]
show :: A where (Show A) => A -> ()
show : x -> intrinsic "display" (Show x)

snippet "Show" : show 'value

instance (Show Text) : it
instance (Show Number) : n -> semantics "pure" (intrinsic "number-to-text" n)
instance (Show Boolean) : x? -> if x? "True" "False"
instance (Show None-Error) : just "found `None`"

Value where (Show Value) => instance (Show (Maybe Value)) : x? -> when x? {
  Some x -> "Some _" x
  None -> "None"
}

Success Failure where (Show Success) (Show Failure) => instance (Show (Result Success Failure)) : x? -> when x? {
  OK x -> "OK _" x
  Error e -> "Error _" e
}

Success Failure where (Show Failure) => instance (Expect (Result Success Failure) Success) : message -> x? -> when x? {
  OK x -> x
  Error e -> error ("_: _" message e)
}

[help "Implement the `read` function."]
[help-group "Input and Output"]
[on-unimplemented ("cannot produce a _ value from `Text`" A)]
Read : A => trait (Text -> Maybe A)

[help "Parse `Text` into a value."]
[help-group "Input and Output"]
read :: A where (Read A) => Text -> Maybe A
read : Read

instance (Read Text) : Some
instance (Read Number) : text -> semantics "pure" (intrinsic "text-to-number" text)

[help-group "Input and Output"]
[help "Read a value from the user with the provided prompt."]
prompt :: A where (Read A) => Text -> A
prompt : prompt -> intrinsic "prompt" prompt Read

[help "Ask the user to pick from a set of options."]
[help-group "Input and Output"]
choice :: Element where (Show Element) => Text -> List Element -> Element
choice : prompt items -> {
  descriptions : items . transform Show . collect :: List Text
  index : intrinsic "choice" prompt descriptions
  items . nth index . unwrap
}
