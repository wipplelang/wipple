[[no-std]]

use "util"
use "fp"
use "mutable"

[help "Create a `List` with the specified elements."]
[help-group "Collections"]
list : syntax { list ...x -> (intrinsic "make-empty-list" :: List _) ...(. append 'x) }

instance (Default (List _)) : list

[help "Retrieve the first item inside a `List`, if it exists."]
[help-group "Collections"]
first :: A => List A -> Maybe A
first : list -> intrinsic "list-first" list

[help "Retrieve the last item inside a `List`, if it exists."]
[help-group "Collections"]
last :: A => List A -> Maybe A
last : list -> intrinsic "list-last" list

[help "Retrieve all but the last item inside a `List`."]
[help-group "Collections"]
initial :: A => List A -> Maybe (List A)
initial : list -> intrinsic "list-initial" list

[help "Retrieve all but the first item inside a `List`."]
[help-group "Collections"]
tail :: A => List A -> Maybe (List A)
tail : list -> intrinsic "list-tail" list

[help "Retrieve the value in a `List` at a particular index, if it exists."]
[help-group "Collections"]
nth :: A => Natural -> List A -> Maybe A
nth : index list -> intrinsic "list-nth" list index

[help "Add a new item to the end of a `List`."]
[help-group "Collections"]
append :: A => A -> List A -> List A
append : new list -> intrinsic "list-append" list new

[help "Add a new item to the end of a `Mutable List`."]
[help-group "Collections"]
append! :: A => A -> Mutable (List A) -> ()
append! : new list -> list . set! (list . get . append new)

[help "Insert a new item at a particular index inside a `List`."]
[help-group "Collections"]
insert-at :: A => Natural -> A -> List A -> Maybe (List A)
insert-at : index new list -> intrinsic "list-insert-at" list index new

[help "Insert a new item at a particular index inside a `Mutable List`."]
[help-group "Collections"]
insert-at! :: A => Natural -> A -> Mutable (List A) -> Boolean
insert-at! : index new list -> when (list . get . insert-at index new) {
  Some new-list -> {
    list . set! new-list
    True
  }
  None -> False
}

[help "Remove the item at a particular index inside a `List`."]
[help-group "Collections"]
remove-at :: A => Natural -> List A -> Maybe (List A)
remove-at : index list -> intrinsic "list-remove-at" list index

[help "Remove the item at a particular index inside a `Mutable List`."]
[help-group "Collections"]
remove-at! :: Natural -> Mutable (List _) -> Boolean
remove-at! : index list -> when (list . get . remove-at index) {
  Some new-list -> {
    list . set! new-list
    True
  }
  None -> False
}
