-- The distance (in pixels) the turtle should move.
Distance : type {pixels :: Number}

-- Specify a distance in pixels.
[unit] pixels :: Number -> Distance
pixels : pixels -> Distance {pixels : pixels}

-- Missing `pixels` after [`n`].
--
-- Double-check your parentheses.
[error] instance (Mismatched (n :: Number) Distance)

-- Move the turtle forward by a distance.
forward :: Distance -> ()
forward : (Distance {pixels : pixels}) -> external "forward" pixels

-- Move the turtle backward by a distance.
backward :: Distance -> ()
backward : (Distance {pixels : pixels}) -> external "backward" pixels
-- An arc.
Arc : type {
  radius :: Distance
  angle :: Angle
}

-- Move the turtle along an arc.
arc :: Arc -> ()
arc : (Arc {
  radius : (Distance {pixels : radius})
  angle : (Angle {degrees : angle})
}) -> external "arc" (radius; angle)

-- Rotate the turtle counterclockwise by an angle.
left :: Angle -> ()
left : (Angle {degrees : degrees}) -> external "left" degrees

-- Rotate the turtle clockwise by an angle.
right :: Angle -> ()
right : (Angle {degrees : degrees}) -> external "right" degrees
-- Go to a position on the screen without drawing.
go :: Distance Distance -> ()
go : (Distance {pixels : x}) (Distance {pixels : y}) -> external "go" (x; y)

-- Set the pen color.
color :: Color -> ()
color : (Color {color : color}) -> external "color" color

-- Draw a path and fill it with a color.
fill :: Color {result} -> result
fill : (Color {color : color}) body -> do {
  external "beginPath" ()
  result : do body
  external "endPath" color
  result
}

-- Set the speed of the turtle, from `0` to `100`. The default speed is `50`.
speed :: Number -> ()
speed : value -> external "speed" value

-- An animal to use instead of the turtle.
Animal : type {name :: String}

animal-widget :: String -> Animal
animal-widget : name -> Animal {name : name}

-- Change the animal being displayed.
animal :: Animal -> ()
animal : (Animal {name : name}) -> external "animal" name
