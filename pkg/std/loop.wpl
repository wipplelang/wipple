[[no-std]]

use "mutable.wpl"

Control-Flow : A => type {
	Break A
	Continue
}

use Control-Flow

loop' :: A => (() -> Control-Flow A) -> A
loop' : body -> when (body ()) {
	Break value -> value
	Continue -> loop' body
}

[keyword]
loop : syntax {
	loop 'body -> loop' (() -> 'body)
}

[keyword]
while : syntax {
	while 'condition 'body -> loop {
		if 'condition {
			'body
			Continue :: Control-Flow ()
		} {
			Break ()
		}
	}
}

[keyword]
unless : syntax {
	unless 'condition 'body -> while (not 'condition) 'body
}

times :: (Natural -> ()) -> Natural -> ()
times : f -> n -> {
	counter : mutable 0
	loop {
		if (get counter = n) {
			Break ()
		} {
			f (get counter)
			increment! counter
			Continue
		}
	}
}
