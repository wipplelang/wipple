Angle : type {
	deg :: Number
}

degrees' :: Number -> Angle
degrees' : deg -> Angle { deg }

degrees : syntax {
	'n degrees -> degrees' 'n
}

radians' :: Number -> Angle
radians' : radians -> degrees' (radians * 180 / 3.14159)

radians : syntax {
	'n radians -> radians' 'n
}

Pixels : type {
	px :: Number
}

pixels' :: Number -> Pixels
pixels' : px -> Pixels { px }

pixels : syntax {
	'n pixels -> pixels' 'n
}

Turtle : type {
	handle :: UI
}

[context]
turtle :: Turtle
turtle : crash "use of turtle graphics outside `with-turtle`"

with-turtle : syntax {
	with-turtle 'body -> {
		with-ui "/playground/ui/turtle/index.js" (handle -> {
			with (turtle : Turtle { handle }) 'body
		})
	}
}

[help "Set the width of the canvas."]
[help-group "Turtle Graphics"]
width :: Pixels -> ()
width : { px } -> handle of turtle . message "width" px

[help "Set the height of the canvas."]
[help-group "Turtle Graphics"]
height :: Pixels -> ()
height : { px } -> handle of turtle . message "height" px

[help "Set the background color of the canvas."]
[help-group "Turtle Graphics"]
background :: Text -> ()
background : color -> handle of turtle . message "background" color

[help "Set the speed of the turtle."]
[help-group "Turtle Graphics"]
speed :: Number -> ()
speed : speed -> {
	if (speed < 0) (crash "`speed` must be at least 0")
	handle of turtle . message "speed" speed
}

[help "Set the x-y position of the turtle."]
[help-group "Turtle Graphics"]
position :: (Pixels , Pixels) -> ()
position : ({ px : x } , { px : y }) -> {
	handle of turtle . message "position-x" x
	handle of turtle . message "position-y" y
}

[help "Lift the pen up, allowing the turtle to move without drawing."]
[help-group "Turtle Graphics"]
pen-up :: () -> ()
pen-up : () -> handle of turtle . message "pen-up" ()

[help "Put the pen back down so the turtle can draw again."]
[help-group "Turtle Graphics"]
pen-down :: () -> ()
pen-down : () -> handle of turtle . message "pen-down" ()

[help "Set the pen color."]
[help-group "Turtle Graphics"]
color :: Text -> ()
color : color -> handle of turtle . message "color" color

[help "Fill the most recent path with a color."]
[help-group "Turtle Graphics"]
fill :: Text -> ()
fill : color -> handle of turtle . message "fill" color

[help "Move the turtle forward by the specified number of pixels."]
[help-group "Turtle Graphics"]
forward :: Pixels -> ()
forward : { px } -> handle of turtle . message "forward" px

[help "Move the turtle backward by the specified number of pixels."]
[help-group "Turtle Graphics"]
backward :: Pixels -> ()
backward : { px } -> handle of turtle . message "backward" px

[help "Turn the turtle left by the specified angle."]
[help-group "Turtle Graphics"]
left :: Angle -> ()
left : { deg } -> handle of turtle . message "left" deg

[help "Turn the turtle right by the specified angle."]
[help-group "Turtle Graphics"]
right :: Angle -> ()
right : { deg } -> handle of turtle . message "right" deg
