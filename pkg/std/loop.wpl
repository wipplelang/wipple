[[no-std]]

use "mutable"
use "async"

Control-Flow : A => type {
	Break A
	Continue
}

loop' :: A => (() -> Control-Flow A) -> A
loop' : body -> when (body ()) {
	Break value -> value
	Continue -> loop' body
}

[keyword]
loop : syntax {
	loop 'body -> loop' (() -> 'body)
}

[keyword]
while : syntax {
	while 'condition 'body -> loop {
		if 'condition {
			'body
			Continue :: Control-Flow ()
		} {
			Break ()
		}
	}
}

[keyword]
unless : syntax {
	unless 'condition 'body -> while (not 'condition) 'body
}

[keyword]
forever : syntax {
	forever 'body -> loop {
		'body :: ()
		Continue
	}
}

[keyword]
loop-every : syntax {
	loop-every 'duration 'body -> loop {
		when 'body {
			Continue -> {
				delay 'duration
				Continue
			}
			Break value -> Break value
		}
	}
}

[help "Repeatedly call a function the specified number of times."]
[help-group "Control Flow"]
times :: (Natural -> ()) -> Natural -> ()
times : f n -> {
	counter : mutable 0
	loop {
		if (get counter = n) {
			Break ()
		} {
			f (get counter)
			increment! counter
			Continue
		}
	}
}
