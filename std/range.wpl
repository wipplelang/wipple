[[no-std]]

use "iterate"

[help "A continuous range between two numbers."]
[help-group "Math"]
Range : N => type {
  min :: N
  max :: N
}

[help "A range between two numbers that counts up by a certain number."]
Stride : N => type {
  min :: N
  max :: N
  step :: N
}

N where (Numeric N) (Order N) (Add N N N) => instance (Iterate (Stride N) N) : range -> {
  { zero } : Numeric
  { min max step } : range
  current : mutable min

  if (max < min) { crash "`max` must be greater than `min`" }

  if (step < zero) { crash "`step` must be positive" }

  iterator (() -> {
    if (get current >= max) { None } {
      n : get current
      current . add! step
      Some n
    }
  })
}

[help "Form a range or stride between two numbers."]
[help-group "Math"]
to : syntax {
  'min to 'max by 'step -> Stride {
    min : 'min
    max : 'max
    step : 'step
  }
  'min to 'max -> Range {
    min : 'min
    max : 'max
  }
}
