-- A continuous range between two numbers. Each bound may be `nan` to
-- indicate that the range is unbounded in that direction.
Range : index => type {
  min :: index
  max :: index
}

To : index => trait (index index -> Range index)
instance (To Number) : min max -> Range {
  min : min
  max : max
}

-- A range between two numbers that counts up in discrete steps.
Stride : index => type {
  min :: index
  max :: index
  step :: index
}

By : index => trait ((Range index) index -> Stride index)
instance (By Number) : (Range {
  min : min
  max : max
}) step -> Stride {
  min : min
  max : max
  step : step
}

instance (As-Sequence (Stride Number) Number) : (Stride {
  min : min
  max : max
  step : step
}) -> when ((min /= nan); (max /= nan)) {
  (True; True) -> do {
    current : min
    sequence {
      if (current < max) {
        n : current
        set current : current + step
        Some n
      } {
        None
      }
    }
  }
  (True; False) -> do {
    current : min
    sequence {
      n : current
      set current : current + step
      Some n
    }
  }
}
