Angle : type {
	degrees :: Number
}

[help "Produce an angle in degrees."]
[help-group "Turtle Graphics"]
degrees :: Number -> Angle
degrees : degrees -> Angle { degrees }

[help "Produce an angle in radians."]
[help-group "Turtle Graphics"]
radians :: Number -> Angle
radians : radians -> degrees (radians * 180 / 3.14159)


Turtle : type {
	handle :: UI
}

[context]
turtle :: Turtle
turtle : crash "use of turtle graphics outside `with-turtle`"

with-turtle : syntax {
	with-turtle 'body -> {
		with-ui "/playground/ui/turtle/index.js" (handle -> {
			with (turtle : Turtle { handle }) 'body
		})
	}
}

[help "Set the width of the canvas."]
[help-group "Turtle Graphics"]
width :: Number -> ()
width : width -> handle of turtle . message "width" width

[help "Set the height of the canvas."]
[help-group "Turtle Graphics"]
height :: Number -> ()
height : height -> handle of turtle . message "height" height

[help "Set the background color of the canvas."]
[help-group "Turtle Graphics"]
background :: Text -> ()
background : color -> handle of turtle . message "background" color

[help "Set the x-y position of the turtle."]
[help-group "Turtle Graphics"]
position :: (Number , Number) -> ()
position : (x , y) -> {
	handle of turtle . message "position-x" x
	handle of turtle . message "position-y" y
}

[help "Lift the pen up, allowing the turtle to move without drawing."]
[help-group "Turtle Graphics"]
pen-up :: () -> ()
pen-up : () -> handle of turtle . message "pen-up" ()

[help "Put the pen back down so the turtle can draw again."]
[help-group "Turtle Graphics"]
pen-down :: () -> ()
pen-down : () -> handle of turtle . message "pen-down" ()

[help "Set the pen color."]
[help-group "Turtle Graphics"]
color :: Text -> Turtle -> ()
color : color -> handle of turtle . message "color" color

[help "Fill a previously created `path` with a color."]
[help-group "Turtle Graphics"]
fill :: Text -> ()
fill : color -> handle of turtle . message "fill" color


path : syntax {
	path 'body -> {
		handle of turtle . message "begin-path" ()
		'body
		handle of turtle . message "close-path" ()
	}
}


[help "Move the turtle forward by the specified amount."]
[help-group "Turtle Graphics"]
forward :: Number -> ()
forward : px -> handle of turtle . message "forward" px


[help "Move the turtle backward by the specified amount."]
[help-group "Turtle Graphics"]
backward :: Number -> ()
backward : px -> handle of turtle . message "backward" px

[help "Turn the turtle left by the specified angle."]
[help-group "Turtle Graphics"]
left :: Angle -> ()
left : { degrees } -> handle of turtle . message "left" degrees

[help "Turn the turtle right by the specified angle."]
[help-group "Turtle Graphics"]
right :: Angle -> ()
right : { degrees } -> handle of turtle . message "right" degrees
