@top Program { expression* }

expression { List | Comment | Placeholder | QuoteName | RepeatName | Text | Number | Asset | Keyword | Operator | Type | Name }

List { "(" expression* ")" }

Keyword { @specialize<Name, "_" | "use" | "when" | "type" | "trait" | "instance" | "where" | "external" | "syntax" | "format" | "with" | "intrinsic" | "semantics" | "snippet"> }

Operator { @specialize<OperatorName, ":" | "::" | "->" | "=>" | "."> }

@tokens {
  "[" "]"
  "(" ")"
  "{" "}"

  Space { $[ \t\n\r]+ }
  LineContinue { "\\" }

  Comment { "[" ![\]]* "]"}
  Placeholder { "{%" ![\{\}%]* "%}" }
  QuoteName { "'" $[A-Za-z0-9\-_]+ $[!?']? }
  RepeatName { "..." $[A-Za-z0-9\-_]+ $[!?']? }
  Text { '"' (!["\\] | "\\" _)* '"' }
  Number { "-"? $[0-9]+ ("." $[0-9]+)? }
  Asset { '`' (![`\\] | "\\" _)* '`' }
  Type { $[A-Z] ![\r\n\t \(\)\[\]\{\}'"/]* }
  OperatorName { $[~`!@#$%^&*\-+=\\\|:;<,>.?/]+ }
  Name { $[A-Za-z0-9\-_!?']+ }

  @precedence { Space, LineContinue, Comment, Placeholder, Text, Number, Asset, QuoteName, RepeatName, Type, OperatorName, Name }
}

@skip { Space | LineContinue }

@detectDelim
