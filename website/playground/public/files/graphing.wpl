[[help-url "https://wipple.dev/doc/graphing.html"]]

Graph : type { handle :: UI }

[context]
graph :: Graph
graph : crash "use of graphing outside `with-graph`"

[help "Create a new graph."]
[help-group "Graphing"]
with-graph : syntax { with-graph 'body -> { with-ui "https://wipple.dev/playground/ui/graphing/index.js" (handle -> { with (graph : Graph { handle }) 'body }) } }

[help "Set the color of the graph."]
[help-group "Graphing"]
color :: Text -> ()
color : color -> handle of graph . message "color" color

[help "Set the minimum x-value displayed by the graph."]
[help-group "Graphing"]
min-x :: Number -> ()
min-x : n -> handle of graph . message "min-x" n

[help "Set the minimum y-value displayed by the graph."]
[help-group "Graphing"]
min-y :: Number -> ()
min-y : n -> handle of graph . message "min-y" n

[help "Set the maximum x-value displayed by the graph."]
[help-group "Graphing"]
max-x :: Number -> ()
max-x : n -> handle of graph . message "max-x" n

[help "Set the maximum y-value displayed by the graph."]
[help-group "Graphing"]
max-y :: Number -> ()
max-y : n -> handle of graph . message "max-y" n

[help "Set the space between individual points on the graph. A higher resolution will be more accurate, but will take longer to compute."]
[help-group "Graphing"]
resolution :: Number -> ()
resolution : n -> handle of graph . message "resolution" n

[help "Add a function to the graph."]
[help-group "Graphing"]
plot :: (Number -> Number) -> ()
plot : f -> handle of graph . message "function" f
