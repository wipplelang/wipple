-- The distance (in pixels) the turtle should move.
Distance : type Number

instance (Describe-Type Distance "distance")

-- Specify a distance in pixels.
@(highlight-category : "unit")
pixels :: Number -> Distance
pixels : Distance

_ where (Error ("Missing `pixels` after `_`" Source ; Error-Description ("Try rewriting this code as `(_ pixels)`, or double-check your parentheses." Source))) =>
  instance (Mismatch Number Distance)

-- Move the turtle forward by a distance.
@(highlight-category : "turtle-movement")
@(highlight-icon : "arrow-upward")
forward :: Distance -> Unit
forward : (Distance pixels) -> external "forward" pixels

-- Move the turtle backward by a distance.
@(highlight-category : "turtle-movement")
@(highlight-icon : "arrow-downward")
backward :: Distance -> Unit
backward : (Distance pixels) -> external "backward" pixels

-- An arc.
Arc : type {
  radius :: Distance
  angle :: Angle
}

-- Move the turtle along an arc.
@(highlight-category : "turtle-movement")
@(highlight-icon : "progress-activity")
arc :: Arc -> Unit
arc : {
  radius : (Distance radius)
  angle : (Angle angle)
} -> external "arc" (radius ; angle)

-- Rotate the turtle counterclockwise by an angle.
@(highlight-category : "turtle-movement")
@(highlight-icon : "rotate-left")
left :: Angle -> Unit
left : (Angle degrees) -> external "left" degrees

-- Rotate the turtle clockwise by an angle.
@(highlight-category : "turtle-movement")
@(highlight-icon : "rotate-right")
right :: Angle -> Unit
right : (Angle degrees) -> external "right" degrees

-- Set the pen color.
@(highlight-category : "turtle-appearance")
@(highlight-icon : "edit")
color :: Color -> Unit
color : (Color color) -> external "color" color

-- Draw a path and fill it with a color.
@(highlight-category : "turtle-appearance")
@(highlight-icon : "colors")
fill :: Result => Color {Result} -> Result
fill : (Color color) body -> do {
  external "beginPath" Unit
  result : do body
  external "endPath" color
  result
}

-- Set the speed of the turtle, from `0` to `100`. The default speed is `50`.
@(highlight-category : "turtle-appearance")
@(highlight-icon : "speed")
speed :: Number -> Unit
speed : value -> external "speed" value

-- An animal to use instead of the turtle.
Animal : type Text
instance (Describe-Type Animal "animal")

-- Change the animal being displayed.
@(highlight-category : "turtle-appearance")
@(highlight-icon : "pets")
animal :: Animal -> Unit
animal : (Animal name) -> external "animal" name
