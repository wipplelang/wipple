-- The number returned from calculations whose result is undefined (eg. dividing
-- by zero).
undefined :: Number
undefined : intrinsic "undefined-number"

-- Add two values together, returning the sum.
@(language : "add")
Add : (Left : Number) (Right : Number) (infer Sum) => trait (Left Right -> Sum)
instance (Add Number Number Number) : left right -> intrinsic "add-number" left right

Left Right Sum where (Error ("Can't add _ and _" Left Right ; Error-Description ("Adding these two items together isn't supported."))) =>
  default instance (Add Left Right Sum) : ...

Right Sum where (Error ("Missing parentheses around the inputs to `+`" ; Error-Description ("Spaces group before operators do, so you need to put parentheses around just the inputs to `+`."))) =>
  instance (Add None Right Sum) : ...

-- Subtract the one value from another, returning the difference.
@(language : "subtract")
Subtract : (Left : Number) (Right : Number) (infer Difference) => trait (Left Right -> Difference)
instance (Subtract Number Number Number) : left right -> intrinsic "subtract-number" left right

Left Right Difference where (Error ("Can't subtract _ by _" Left Right ; Error-Description ("Subtracting these two items isn't supported."))) =>
  default instance (Subtract Left Right Difference) : ...

Right Difference where (Error ("Missing parentheses around the inputs to `-`" ; Error-Description ("Spaces group before operators do, so you need to put parentheses around just the inputs to `-`."))) =>
  instance (Subtract None Right Difference) : ...

-- Multiply two values together, returning the product.
@(language : "multiply")
Multiply : (Left : Number) (Right : Number) (infer Product) => trait (Left Right -> Product)
instance (Multiply Number Number Number) : left right -> intrinsic "multiply-number" left right

Left Right Product where (Error ("Can't multiply _ and _" Left Right ; Error-Description ("Multiplying these two items together isn't supported."))) =>
  default instance (Multiply Left Right Product) : ...

Right Product where (Error ("Missing parentheses around the inputs to `*`" ; Error-Description ("Spaces group before operators do, so you need to put parentheses around just the inputs to `*`."))) =>
  instance (Multiply None Right Product) : ...

-- Divide two numbers, returning the quotient.
@(language : "divide")
Divide : (Left : Number) (Right : Number) (infer Quotient) => trait (Left Right -> Quotient)
instance (Divide Number Number Number) : left right -> intrinsic "divide-number" left right

Left Right Quotient where (Error ("Can't divide _ by _" Left Right ; Error-Description ("Dividing these two items isn't supported."))) =>
  default instance (Divide Left Right Quotient) : ...

Right Quotient where (Error ("Missing parentheses around the inputs to `/`" ; Error-Description ("Spaces group before operators do, so you need to put parentheses around just the inputs to `/`."))) =>
  instance (Divide None Right Quotient) : ...

-- Divide two numbers, returning the remainder.
@(language : "remainder")
Remainder : (Left : Number) (Right : Number) (infer Remainder) => trait (Left Right -> Remainder)
instance (Remainder Number Number Number) : left right -> intrinsic "remainder-number" left right

Left Right Remainder where (Error ("Can't divide _ by _ and get the remainder" Left Right ; Error-Description ("Dividing these two items with remainder isn't supported."))) =>
  default instance (Remainder Left Right Remainder) : ...

Right Remainder where (Error ("Missing parentheses around the inputs to `%`" ; Error-Description ("Spaces group before operators do, so you need to put parentheses around just the inputs to `%`."))) =>
  instance (Remainder None Right Remainder) : ...

-- Raise one number to the power of the other number.
@(language : "power")
Power : (Left : Number) (Right : Number) (infer Power) => trait (Left Right -> Power)
instance (Power Number Number Number) : left right -> intrinsic "power-number" left right

Left Right Power where (Error ("Can't raise _ to the power of _" Left Right ; Error-Description ("Exponentiating these two items isn't supported."))) =>
  default instance (Power Left Right Power) : ...

Right Power where (Error ("Missing parentheses around the inputs to `^`" ; Error-Description ("Spaces group before operators do, so you need to put parentheses around just the inputs to `^`."))) =>
  instance (Power None Right Power) : ...

-- Round down a number to the nearest integer.
floor :: Number -> Number
floor : n -> intrinsic "floor-number" n

-- Round up a number to the nearest integer.
ceil :: Number -> Number
ceil : n -> intrinsic "ceil-number" n

-- Calculate the square root of a number.
sqrt :: Number -> Number
sqrt : n -> intrinsic "sqrt-number" n

-- An angle represented in degrees.
Angle : type Number

instance (Describe-Type Angle "angle")

-- An angle in degrees.
@(highlight-category : "unit")
degrees :: Number -> Angle
degrees : Angle

_ where (Error ("Missing `degrees` after _" Source ; Error-Description ("Try rewriting this code as `(_ degrees)`, or double-check your parentheses." Source))) =>
  instance (Mismatch Number Angle)

-- Calculate the sine of an angle.
sin :: Angle -> Number
sin : (Angle degrees) -> intrinsic "sin" degrees

-- Calculate the cosine of an angle.
cos :: Angle -> Number
cos : (Angle degrees) -> intrinsic "cos" degrees

-- Calculate the tangent of an angle.
tan :: Angle -> Number
tan : (Angle degrees) -> intrinsic "tan" degrees

-- Negate the sign of a number.
negate :: Number -> Number
negate : n -> intrinsic "negate-number" n

-- Check if the first number is divisible by the second.
divisible-by? :: Number -> Number -> Boolean
divisible-by? : right -> left -> left % right = 0
