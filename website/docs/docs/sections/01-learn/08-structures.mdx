## Structures

So far, we've been writing programs that deal with "simple" data — numbers, text, functions, and so on. Now, we'll learn how to model complex data using **structures**!

Let's start by modeling a `Person`. We can create our own structure type like so:

```wipple
Person : type {
    name :: Text
    age :: Number
}
```

Now let's actually create a `Person` value:

```wipple
bob : Person {
    name : "Bob"
    age : 30
}
```

To retrieve the `name` and the `age` from `bob`, you can use **destructuring**:

```wipple
{ name age } : bob

show name
show age
```

You can use destructuring inside functions, too!

```wipple
describe :: Person -> Text
describe : { name age } -> format "_ is _ years old" name age

show (describe bob) -- Bob is 30 years old
```

How about a more complicated example? Let's add in type functions!

```wipple
Pair : A B => type {
    first :: A
    second :: B
}

my-pair :: Pair (Pair Text Number) Number
my-pair : Pair {
    first : Pair {
        first : "abc"
        second : 1
    }
    second : 2
}
```

Let's conclude our journey with types by talking about **traits**!
