-- Produce a piece of `Text` describing the value.
Describe : (Value : Text) => trait (Value -> Text)

Value where (Error ("cannot describe a _ value" Value)) =>
    default instance (Describe Value) : ...

intrinsic "constant" "show" : Describe -- FIXME: Rename intrinsic to 'describe'

-- Produce a piece of `Text` describing the value.
describe :: (Value : Text) where (Describe Value) => Value -> Text
describe : Describe

-- Display a value on the screen.
show :: (Value : Text) where (Describe Value) => Value -> ()
show : value -> intrinsic "display" (describe value)

instance (Highlight show (Highlight-Category "io" ; Highlight-Icon "article")) : ...

print :: (Value : Text) where (Error "use `show` to display on the screen") => Value -> ()
print : ...

println :: (Value : Text) where (Error "use `show` to display on the screen") => Value -> ()
println : ...

display :: (Value : Text) where (Error "use `show` to display on the screen") => Value -> ()
display : ...

instance (Describe Text) : text -> text
instance (Describe Number) : n -> intrinsic "number-to-text" n
instance (Describe Boolean) : b -> if b {"True"} {"False"}
Value where (Describe Value) => instance (Describe (Maybe Value)) : m -> when m {
    Some value -> "Some _" value
    None -> "None"
}

-- Convert from a piece of `Text` to a value.
Read : Value => trait (Text -> Maybe Value)

Value where (Error ("cannot produce a _ value from _" Value Text)) =>
    default instance (Read Value) : ...

instance (Read Text) : Some
instance (Read Number) : text -> intrinsic "text-to-number" text

-- Ask the user to enter a value.
prompt :: Value where (Read Value) => Text -> Value
prompt : prompt -> intrinsic "prompt" prompt Read

instance (Highlight prompt (Highlight-Category "io" ; Highlight-Icon "live-help")) : ...

-- -- Ask the user to pick from a list of choices.
choice :: Element where (Describe Element) => Text (List Element) -> Element
choice : prompt items -> do {
    descriptions : items . transform Describe . collect :: List _
    index : intrinsic "choice" prompt descriptions
    (items . nth index) or unreachable
}

instance (Highlight choice (Highlight-Category "io" ; Highlight-Icon "format-list-bulleted")) : ...
