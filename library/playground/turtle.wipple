-- The distance (in pixels) the turtle should move.
-- [description]: "distance in `pixels`"
-- [on-mismatch `Number`]: "missing unit for distance"
-- [fix-mismatch `Number`]: "add `pixels`" `(_ pixels)`
Distance : type Number

-- Specify a distance in pixels.
pixels :: Number -> Distance
pixels : Distance

-- Move the turtle forward by the specified distance.
forward :: Distance -> ()
forward : (Distance pixels) -> runtime-message "forward" pixels

-- Move the turtle backward by the specified distance.
backward :: Distance -> ()
backward : (Distance pixels) -> runtime-message "backward" pixels

-- An arc.
Arc : type {
    radius :: Distance
    angle :: Angle
}

-- Move the turtle along an arc.
arc :: Arc -> ()
arc : {
    radius : (Distance radius)
    angle : (Angle angle)
} -> runtime-message "arc" (radius ; angle)

-- Rotate the turtle counterclockwise by the specified angle.
left :: Angle -> ()
left : (Angle degrees) -> runtime-message "left" degrees

-- Rotate the turtle clockwise by the specified angle.
right :: Angle -> ()
right : (Angle degrees) -> runtime-message "right" degrees

-- Set the pen color.
color :: Color -> ()
color : (Color color) -> runtime-message "color" color

-- Draw a path and fill it with the specified color.
fill :: Result => Color {Result} -> Result
fill : (Color color) body -> do {
    runtime-message "begin-path" ()
    result : do body
    runtime-message "end-path" color
    result
}

-- The speed of the turtle, from `very-slow` to `very-fast`.
Speed : type Number

-- A very slow speed.
very-slow :: Speed
very-slow : Speed 0

-- A slow speed.
slow :: Speed
slow : Speed 0.25

-- A fast speed.
fast :: Speed
fast : Speed 0.75

-- A very fast speed.
very-fast :: Speed
very-fast : Speed 1

-- Set the speed of the turtle.
speed :: Speed -> ()
speed : (Speed value) -> runtime-message "speed" value
