-- The distance (in pixels) the turtle should move.
Distance : type Number

instance (Describe-Type Distance "distance")

-- Specify a distance in pixels.
@(highlight-category : "unit")
pixels :: Number -> Distance
pixels : Distance

() where (Error "missing-pixels") =>
  instance (Mismatch Number Distance)

-- Move the turtle forward by the specified distance.
@(highlight-category : "turtle-movement")
@(highlight-icon : "arrow-upward")
forward :: Distance -> Unit
forward : (Distance pixels) -> runtime-message "forward" pixels

-- Move the turtle backward by the specified distance.
@(highlight-category : "turtle-movement")
@(highlight-icon : "arrow-downward")
backward :: Distance -> Unit
backward : (Distance pixels) -> runtime-message "backward" pixels

-- An arc.
Arc : type {
  radius :: Distance
  angle :: Angle
}

-- Move the turtle along an arc.
@(highlight-category : "turtle-movement")
@(highlight-icon : "progress-activity")
arc :: Arc -> Unit
arc : {
  radius : (Distance radius)
  angle : (Angle angle)
} -> runtime-message "arc" (radius ; angle)

-- Rotate the turtle counterclockwise by the specified angle.
@(highlight-category : "turtle-movement")
@(highlight-icon : "rotate-left")
left :: Angle -> Unit
left : (Angle degrees) -> runtime-message "left" degrees

-- Rotate the turtle clockwise by the specified angle.
@(highlight-category : "turtle-movement")
@(highlight-icon : "rotate-right")
right :: Angle -> Unit
right : (Angle degrees) -> runtime-message "right" degrees

-- Set the pen color.
@(highlight-category : "turtle-appearance")
@(highlight-icon : "edit")
color :: Color -> Unit
color : (Color color) -> runtime-message "color" color

-- Draw a path and fill it with the specified color.
@(highlight-category : "turtle-appearance")
@(highlight-icon : "colors")
fill :: Result => Color {Result} -> Result
fill : (Color color) body -> do {
  runtime-message "begin-path" ()
  result : do body
  runtime-message "end-path" color
  result
}

-- The speed of the turtle, from `very-slow` to `very-fast`.
Speed : type Number

instance (As Number Speed) : Speed

-- A very slow speed.
very-slow :: Speed
very-slow : Speed 0

-- A slow speed.
slow :: Speed
slow : Speed 0.25

-- The default speed.
normal :: Speed
normal : Speed 0.5

-- A fast speed.
fast :: Speed
fast : Speed 0.75

-- A very fast speed.
very-fast :: Speed
very-fast : Speed 1

-- Set the speed of the turtle.
@(highlight-category : "turtle-appearance")
@(highlight-icon : "speed")
speed :: Speed -> Unit
speed : (Speed value) -> runtime-message "speed" value

-- An animal to use instead of the turtle.
Animal : type Text
instance (Describe-Type Animal "animal")

-- Change the animal being displayed.
@(highlight-category : "turtle-appearance")
@(highlight-icon : "pets")
animal :: Animal -> Unit
animal : (Animal name) -> runtime-message "animal" name
