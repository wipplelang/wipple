-- The number returned from calculations whose result is not a number (eg.
-- dividing by zero).
nan :: Number
nan : intrinsic "nan"

-- Add two values together, returning the sum.
Add : left right (infer sum) => trait (left right -> sum)
instance (Add Number Number Number) : left right -> intrinsic "add-numbers" left right

-- Can't add [`left`] and [`right`].
--
-- Adding these two items together isn't supported.
[default] [error] instance (Add left right sum)

-- Missing parentheses around the inputs to `+`.
--
-- Spaces group before operators do, so you need to put parentheses around just
-- the inputs to `+`.
[error] instance (Add () right sum)

-- Subtract one value from another, returning the difference.
Subtract : left right (infer difference) => trait (left right -> difference)
instance (Subtract Number Number Number) : left right -> intrinsic "subtract-numbers" left right

-- Can't subtract [`right`] from [`left`].
--
-- Subtracting these two items isn't supported.
[default] [error] instance (Subtract left right difference)

-- Missing parentheses around the inputs to `-`.
--
-- Spaces group before operators do, so you need to put parentheses around just
-- the inputs to `-`.
[error] instance (Subtract () right difference)

-- Multiply two values together, returning the product.
Multiply : left right (infer product) => trait (left right -> product)
instance (Multiply Number Number Number) : left right -> intrinsic "multiply-numbers" left right

-- Can't multiply [`left`] and [`right`].
--
-- Multiplying these two items together isn't supported.
[default] [error] instance (Multiply left right product)

-- Missing parentheses around the inputs to `*`.
--
-- Spaces group before operators do, so you need to put parentheses around just
-- the inputs to `*`.
[error] instance (Multiply () right product)

-- Divide two numbers, returning the quotient.
Divide : left right (infer quotient) => trait (left right -> quotient)
instance (Divide Number Number Number) : left right -> intrinsic "divide-numbers" left right

-- Can't divide [`left`] by [`right`].
--
-- Dividing these two items isn't supported.
[default] [error] instance (Divide left right quotient)

-- Missing parentheses around the inputs to `/`.
--
-- Spaces group before operators do, so you need to put parentheses around just
-- the inputs to `/`.
[error] instance (Divide () right quotient)

-- Divide two numbers, returning the remainder.
Remainder : left right (infer remainder) => trait (left right -> remainder)
instance (Remainder Number Number Number) : left right -> intrinsic "remainder-numbers" left right

-- Can't divide [`left`] by [`right`] and get the remainder.
--
-- Dividing these two items with remainder isn't supported.
[default] [error] instance (Remainder left right remainder)

-- Missing parentheses around the inputs to `%`.
--
-- Spaces group before operators do, so you need to put parentheses around just
-- the inputs to `%`.
[error] instance (Remainder () right remainder)

-- Raise one number to the power of the other number.
Power : left right (infer power) => trait (left right -> power)
instance (Power Number Number Number) : left right -> intrinsic "power-numbers" left right

-- Can't raise [`left`] to the power of [`right`].
--
-- Exponentiating these two items isn't supported.
[default] [error] instance (Power left right power)

-- Missing parentheses around the inputs to `^`.
--
-- Spaces group before operators do, so you need to put parentheses around just
-- the inputs to `^`.
[error] instance (Power () right power)

-- Round down a number to the nearest integer.
floor :: Number -> Number
floor : n -> intrinsic "floor-number" n

-- Round up a number to the nearest integer.
ceil :: Number -> Number
ceil : n -> intrinsic "ceil-number" n

-- Calculate the square root of a number.
sqrt :: Number -> Number
sqrt : n -> intrinsic "sqrt-number" n

-- An angle represented in degrees.
Angle : type {degrees :: Number}

-- An angle in degrees.
[unit] degrees :: Number -> Angle
degrees : degrees -> Angle {degrees : degrees}

-- Calculate the sine of an angle.
sin :: Angle -> Number
sin : (Angle {degrees : degrees}) -> intrinsic "sin" degrees

-- Calculate the cosine of an angle.
cos :: Angle -> Number
cos : (Angle {degrees : degrees}) -> intrinsic "cos" degrees

-- Calculate the tangent of an angle.
tan :: Angle -> Number
tan : (Angle {degrees : degrees}) -> intrinsic "tan" degrees

-- Negate the sign of a number.
negate :: Number -> Number
negate : n -> intrinsic "negate-number" n

-- Check if the first number is divisible by the second.
divisible-by? :: Number -> Number -> Boolean
divisible-by? : right -> left -> left % right = 0
